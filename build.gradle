apply plugin: DependenciesPlugin

subprojects {
    buildscript {
        repositories {
            gradlePluginPortal()
        }
        dependencies {
            classpath gradlePlugins.gradleGit
        }
    }

    group = property("GROUP")
    version = property("VERSION_NAME")

    afterEvaluate { project ->
        configurePomDetails(project)
    }

    repositories {
        mavenCentral()
    }

    project.plugins.withType(JavaBasePlugin) {
        project.with {
            sourceCompatibility = JavaVersion.VERSION_1_7
            targetCompatibility = JavaVersion.VERSION_1_7
        }
    }
}

void configurePomDetails(Project project) {
    project.publishing {
        publications.withType(MavenPublication) {
            pom {
                url = findProperty("POM_URL")

                scm {
                    url = findProperty("POM_SCM_URL")
                    connection = findProperty("POM_SCM_CONNECTION")
                }

                licenses {
                    license {
                        name = findProperty("POM_LICENCE_NAME")
                        url = findProperty("POM_LICENCE_URL")
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '5.6.3'
    distributionType = Wrapper.DistributionType.ALL
}
